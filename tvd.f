c     subroutine tvd 
      subroutine tvd ( q, w1, w2, nx )
c-----------------------------------------------------------------------
c  1d tvd code based on Harten (1983)
c  This version uses Dongsu's improved contact discontinuity scheme
c  q     (input/output)
c  w1    (work space)
c  w2    (work space)
c  nx    (input)
c-----------------------------------------------------------------------
      integer nx, i
      real*8 q(5,-1:nx+2), w1(40,-1:nx+2), w2(100,-1:nx+2)
      real*8 gam, gami1, cour, eps, dx, dt, dtdx
c
      common /param/ gam, gami1, cour, eps, dx, dt
c     include "common.inc"
c
c     cour = 0.95
      eps  = 0.1
c-----------------------------------------------------------------------
c  u(i), p(i), H(i)
c-----------------------------------------------------------------------
      do 101 i=-1,nx+2
         w1(1,i) = q(2,i)/q(1,i)
         w1(2,i) = (gam-1.)*(q(3,i)-q(2,i)*w1(1,i)/2.)
         w1(3,i) = (q(3,i)+w1(2,i))/q(1,i)
 101  continue
c-----------------------------------------------------------------------
c  ^u(i+1/2), ^H(i+1/2), ^c(i+1/2)
c-----------------------------------------------------------------------
      do 102 i=-1,nx+1
         w2(1,i) = (sqrt(q(1,i))*w1(1,i)+sqrt(q(1,i+1))*w1(1,i+1))
     +          / (sqrt(q(1,i))+sqrt(q(1,i+1)))
         w2(2,i) = (sqrt(q(1,i))*w1(3,i)+sqrt(q(1,i+1))*w1(3,i+1))
     +          / (sqrt(q(1,i))+sqrt(q(1,i+1)))
         w2(3,i) = sqrt((gam-1.)*(w2(2,i)-w2(1,i)**2/2.))
 102  continue
c-----------------------------------------------------------------------
c  ak(i+1/2), alpk(i+1/2), R1k(i+1/2), R2k(i+1/2), R3k(i+1/2)
c-----------------------------------------------------------------------
      do 103 i=-1,nx+1
         w2(11,i) = w2(1,i)-w2(3,i)
         w2(12,i) = w2(1,i)
         w2(13,i) = w2(1,i)+w2(3,i)
         w2(21,i) = (gam-1.)*((q(3,i+1)-q(3,i))
     +                      +w2(1,i)**2/2.*(q(1,i+1)-q(1,i))
     +                      -w2(1,i)*(q(2,i+1)-q(2,i)))/w2(3,i)**2/2.
     +           - ((q(2,i+1)-q(2,i))-w2(1,i)*(q(1,i+1)-q(1,i)))
     +             /w2(3,i)/2.
         w2(22,i) = (q(1,i+1)-q(1,i))
     +           - (gam-1.)*((q(3,i+1)-q(3,i))
     +                      +w2(1,i)**2/2.*(q(1,i+1)-q(1,i))
     +                      -w2(1,i)*(q(2,i+1)-q(2,i)))/w2(3,i)**2
         w2(23,i) = (gam-1.)*((q(3,i+1)-q(3,i))
     +                      +w2(1,i)**2/2.*(q(1,i+1)-q(1,i))
     +                      -w2(1,i)*(q(2,i+1)-q(2,i)))/w2(3,i)**2/2.
     +           + ((q(2,i+1)-q(2,i))-w2(1,i)*(q(1,i+1)-q(1,i)))
     +             /w2(3,i)/2.
         w2(31,i) = 1.
         w2(32,i) = w2(1,i)-w2(3,i)
         w2(33,i) = w2(2,i)-w2(1,i)*w2(3,i)
         w2(41,i) = 1.
         w2(42,i) = w2(1,i)
         w2(43,i) = w2(1,i)**2/2.
         w2(51,i) = 1.
         w2(52,i) = w2(1,i)+w2(3,i)
         w2(53,i) = w2(2,i)+w2(1,i)*w2(3,i)
 103  continue
c-----------------------------------------------------------------------
c  dt
c-----------------------------------------------------------------------
      dt = 0.
      do 104 i=-1,nx+1
         dt = max(dt,abs(w2(11,i)),abs(w2(12,i)),abs(w2(13,i)))
 104  continue
      dt = cour/dt*dx
      dtdx = dt/dx
c-----------------------------------------------------------------------
c  ~gk(i+1/2), sigma2(i+1/2)
c-----------------------------------------------------------------------
      do 105 i=-1,nx+1
         if (dtdx*abs(w2(11,i)).ge.2.*eps) then
           w2(61,i) = (dtdx*abs(w2(11,i))-(dtdx*w2(11,i))**2)
     +                /2.*w2(21,i)
         else
           w2(61,i) = ((dtdx*w2(11,i))**2/4./eps+eps-(dtdx*w2(11,i))**2)
     +                /2.*w2(21,i)
         endif
         if (dtdx*abs(w2(12,i)).ge.2.*eps) then
           w2(62,i) = (dtdx*abs(w2(12,i))-(dtdx*w2(12,i))**2)
     +                /2.*w2(22,i)
         else
           w2(62,i) = ((dtdx*w2(12,i))**2/4./eps+eps
     +                -(dtdx*w2(12,i))**2)
     +                /2.*w2(22,i)
         endif
         if (dtdx*abs(w2(13,i)).ge.2.*eps) then
           w2(63,i) = (dtdx*abs(w2(13,i))-(dtdx*w2(13,i))**2)
     +                /2.*w2(23,i)
         else
           w2(63,i) = ((dtdx*w2(13,i))**2/4./eps+eps-(dtdx*w2(13,i))**2)
     +                /2.*w2(23,i)
         endif
           w2(67,i) = (1.-dtdx*abs(w2(12,i)))/2.
 105  continue
c-----------------------------------------------------------------------
c  theta2(i), --g2(i), gk(i)
c-----------------------------------------------------------------------
      do 106 i=0,nx+1
         if ((abs(w2(22,i))+abs(w2(22,i-1))).ge.1.d-30) then
            w1(12,i) = abs(w2(22,i)-w2(22,i-1))
     +                 /(abs(w2(22,i))+abs(w2(22,i-1)))
         else
            w1(12,i) = 0.
         endif
         w1(22,i) = sign(1.d0,w2(22,i))
     +              *max(0.d0,min(sign(1.d0,w2(22,i))
     +                        *w2(67,i-1)*w2(22,i-1),
     +                        w2(67,i)*abs(w2(22,i))))
         w1(31,i) = sign(1.d0,w2(61,i))
     +              *max(0.d0,min(abs(w2(61,i)),
     +                        sign(1.d0,w2(61,i))*w2(61,i-1)))
         w1(32,i) = sign(1.d0,w2(62,i))
     +              *max(0.d0,min(abs(w2(62,i)),
     +                        sign(1.d0,w2(62,i))*w2(62,i-1)))
     +            + w1(12,i)*w1(22,i)
         w1(33,i) = sign(1.d0,w2(63,i))
     +              *max(0.d0,min(abs(w2(63,i)),
     +                        sign(1.d0,w2(63,i))*w2(63,i-1)))
 106  continue
c-----------------------------------------------------------------------
c  gammak(i+1/2), betak(i+1/2)
c-----------------------------------------------------------------------
      do 107 i=0,nx
         if (abs(w2(21,i)).ge.1.d-30) then
            w2(71,i) = (w1(31,i+1)-w1(31,i))/w2(21,i)
         else
            w2(71,i) = 0.
         endif
         if (abs(w2(22,i)).ge.1.d-30) then
            w2(72,i) = (w1(32,i+1)-w1(32,i))/w2(22,i)
         else
            w2(72,i) = 0.
         endif
         if (abs(w2(23,i)).ge.1.d-30) then
            w2(73,i) = (w1(33,i+1)-w1(33,i))/w2(23,i)
         else
            w2(73,i) = 0.
         endif
         if (abs(dtdx*w2(11,i)+w2(71,i)).ge.2.*eps) then
           w2(81,i) = abs(dtdx*w2(11,i)+w2(71,i))*w2(21,i)
     +              - (w1(31,i)+w1(31,i+1))
         else
           w2(81,i) = ((dtdx*w2(11,i)+w2(71,i))**2/4./eps+eps)*w2(21,i)
     +              - (w1(31,i)+w1(31,i+1))
         endif
         if (abs(dtdx*w2(12,i)+w2(72,i)).ge.2.*eps) then
           w2(82,i) = abs(dtdx*w2(12,i)+w2(72,i))*w2(22,i)
     +              - (w1(32,i)+w1(32,i+1))
         else
           w2(82,i) = ((dtdx*w2(12,i)+w2(72,i))**2/4./eps+eps)
     +               *w2(22,i)
     +              - (w1(32,i)+w1(32,i+1))
         endif
         if (abs(dtdx*w2(13,i)+w2(73,i)).ge.2.*eps) then
           w2(83,i) = abs(dtdx*w2(13,i)+w2(73,i))*w2(23,i)
     +              - (w1(33,i)+w1(33,i+1))
         else
           w2(83,i) = ((dtdx*w2(13,i)+w2(73,i))**2/4./eps+eps)*w2(23,i)
     +              - (w1(33,i)+w1(33,i+1))
         endif
 107  continue
c-----------------------------------------------------------------------
c  -f(i+1/2)
c-----------------------------------------------------------------------
      do 108 i=0,nx
         w2(91,i) = (q(2,i)+q(2,i+1))/2.
         w2(92,i) = (q(2,i)*w1(1,i)+w1(2,i)
     +              +q(2,i+1)*w1(1,i+1)+w1(2,i+1))/2.
         w2(93,i) = (q(3,i)*w1(1,i)+w1(1,i)*w1(2,i)
     +              +q(3,i+1)*w1(1,i+1)+w1(1,i+1)*w1(2,i+1))/2.
         w2(91,i) = w2(91,i) - (w2(81,i)*w2(31,i)
     +                         +w2(82,i)*w2(41,i)
     +                         +w2(83,i)*w2(51,i))/dtdx/2.
         w2(92,i) = w2(92,i) - (w2(81,i)*w2(32,i)
     +                         +w2(82,i)*w2(42,i)
     +                         +w2(83,i)*w2(52,i))/dtdx/2.
         w2(93,i) = w2(93,i) - (w2(81,i)*w2(33,i)
     +                         +w2(82,i)*w2(43,i)
     +                         +w2(83,i)*w2(53,i))/dtdx/2.
 108  continue
c-----------------------------------------------------------------------
c  update q
c-----------------------------------------------------------------------
      do 109 i=1,nx
         q(1,i) = q(1,i) - dtdx*(w2(91,i)-w2(91,i-1))
         q(2,i) = q(2,i) - dtdx*(w2(92,i)-w2(92,i-1))
         q(3,i) = q(3,i) - dtdx*(w2(93,i)-w2(93,i-1))
 109  continue
c
      return
      end
